use "jerimum"

tipo Jogador
  var x, y: Real
  imagem = Imagem("nave.png")
  var ângulo, vel_x, vel_y := 0.0
  var placar := 0

  cate_estrelas(estrelas: Lista[Estrela])
    n = estrelas.tamanho
    estrelas_catadas = estrelas.selecione(
      estrela => Jogo.distância(x, y, estrela.x, estrela.y) > 35
    )
    placar := placar + (n - estrelas_catadas.tamanho) * 10
    estrelas_catadas
  fim

  desenhe()
    imagem.desenhe_centralizado(x, y, 2, ângulo)
  fim

  girar_direita()
    ângulo := ângulo + 5
  fim

  girar_esquerda()
    ângulo := ângulo - 5
  fim

  acelerar()
    vel_x := vel_x + Jogo.projeção_X(ângulo, 0.5)
    vel_y := vel_y + Jogo.projeção_Y(ângulo, 0.5)
  fim

  mover()
    x := x + vel_x
    y := y + vel_y
    x := (x + 640) mod 640
    y := (y + 480) mod 480
    vel_x := vel_x * 0.95
    vel_y := vel_y * 0.95
  fim
fim

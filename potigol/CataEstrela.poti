use "jerimum"

# Tipos
tipo Jogador
  var x, y: Real
  imagem = Imagem("Nave.png")
  var placar = 0
  var vel_x, vel_y = 0.0
  var angulo = 0.0
  
  desenhe()
    imagem.desenhe_centralizado(x, y, 3, angulo)
  fim

  girar_direita()
    angulo := angulo + 5.0
  fim

  girar_esquerda()
    angulo := angulo - 5.0
  fim

  acelerar()
    vel_x := vel_x + Jogo.projecao_X(angulo, 0.5)
    vel_y := vel_y + Jogo.projecao_Y(angulo, 0.5)
  fim

  mover()
    x := x + vel_x
    y := y + vel_y
    x := (jogo.largura + x) mod jogo.largura
    y := (jogo.altura  + y) mod jogo.altura
    vel_x := vel_x * 0.95
    vel_y := vel_y * 0.95
  fim

  catar_estrelas(estrelas: Lista[Estrela])
    catado = estrelas.selecione(
      estrela => Jogo.distância(x, y, estrela.x, estrela.y) >= 35
    )
    n = estrelas.tamanho - catado.tamanho
    placar := placar + n * 10
    catado
  fim
fim

tipo Estrela
  cor: Cor
  x = aleatório(jogo.largura)
  y = aleatório(jogo.altura)
  imagens = Imagem.fatie("Estrela.png", 25, 25)

  desenhe()
    img = imagens[Relogio.milisegundos div 100 mod imagens.tamanho + 1]
    img.desenhe_centralizado(x, y, 1, 0)
  fim
fim

imagem_fundo = Imagem("Space.png")
nave = Jogador(40, 40)
var estado = "INICIO"
var tempo := 0.0
fonte = Fonte(16)

var estrelas = Lista(1,Estrela(Cor.BRANCO))

para i de 1 até 10 faça
  estrelas := Estrela(Cor.BRANCO) :: estrelas
fim

desenhe()
  imagem_fundo.desenhe(0, 0, 0)
  escolha estado
    caso "INICIO"  => desenhe_inicio
    caso "JOGANDO" => desenhe_jogando
    caso _         => desenhe_fim
  fim
fim

atualize()
  escolha estado
    caso "INICIO"  => atualize_inicio
    caso "JOGANDO" => atualize_jogando
    caso _         => atualize_fim
  fim
fim

# Estado: inicio do jogo
atualize_inicio()
  se Teclado.TECLA_I então estado := "JOGANDO" fim
fim

desenhe_inicio()
  msg = "PRESSIONE [I] PARA COMEÇAR"
  fonte.desenhe_centralizado(msg, jogo.largura / 2, jogo.altura / 2, 3, Cor.AMARELO)
fim

# Estado: JOGANDO
atualize_jogando()
  # eventos
  se Teclado.TECLA_PARA_DIREITA  então nave.girar_direita fim
  se Teclado.TECLA_PARA_ESQUERDA então nave.girar_esquerda fim
  se Teclado.TECLA_PARA_CIMA     então nave.acelerar fim   
  # inserir novas estrelas estrelas se necessario
  se aleatório(100) < 4 e estrelas.tamanho < 25 então 
    estrelas := Estrela(Cor.BRANCO) :: estrelas
  fim

  estrelas := nave.catar_estrelas(estrelas)     # catar estrelas
  nave.mover                                    # atualizar a posicao do jogador
  tempo := tempo + 1.0/60.0                        # incrementar o tempo
  se tempo.inteiro >= 30 então estado := "FIM" fim # terminar o jogo depois de 30 segundos
fim

desenhe_jogando()
  nave.desenhe
  para estrela em estrelas faça
    estrela.desenhe
  fim
  fonte.desenhe("Placar: {nave.placar}", 10, 20, 3, Cor.AMARELO)
  fonte.desenhe("Tempo: {tempo.inteiro}s",  10, 40, 3, Cor.AMARELO)
fim

# Estado: fim do jogo
desenhe_fim()
  msg = "FIM DE JOGO, VOCE FEZ {nave.placar} PONTOS"
  fonte.desenhe_centralizado(msg, jogo.largura/2, jogo.altura/2, 3, Cor.AMARELO)
fim

atualize_fim()
fim

# Jogo
jogo.iniciar("Cata Estrelas", 640, 480, atualize, desenhe)
